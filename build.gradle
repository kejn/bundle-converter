apply plugin: 'jacoco'
apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group 'com.github.kejn'
version '1.0.0'

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task packageJavadoc(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

javadoc {
  source = sourceSets.main.allJava
  classpath = configurations.compileClasspath

  options
  {
    setMemberLevel JavadocMemberLevel.PUBLIC
    setAuthor true

    links "https://docs.oracle.com/javase/8/docs/api/"
  }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                withXml {
                    asNode().children().last() + {
                        resolveStrategy = Closure.DELEGATE_FIRST
                        name project.group + ':' + project.name
                        description 'Library allowing easy conversion between .properties and .xlsx file formats.'
                        url 'https://github.com/kejn/bundle-converter'
                        scm {
                            connection 'scm:git:git://github.com/kejn/bundle-converter.git'
                            developerConnection 'scm:git:ssh://github.com/kejn/bundle-converter.git'
                            url 'https://github.com/kejn/bundle-converter'
                        }
                        licenses {
                            license {
                                name 'MIT License'
                                url 'http://www.opensource.org/licenses/mit-license.php'
                                distribution 'repo'
                            }
                        }
                        developers {
                            developer {
                                id 'kejn'
                                name 'Kamil Niemczyk'
                                email 'kaen04@gmail.com'
                            }
                        }
                    }
                }
            }

            from components.java

            artifact sourceJar
            artifact packageJavadoc
        }
    }
}

repositories {
    jcenter()
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

/*
 * Remember to distinguish between 'api' and 'implementation' dependencies
 */
dependencies {
    api 'org.apache.poi:poi-ooxml:3.17'
    implementation 'org.apache.commons:commons-text:1.2'
    implementation 'com.google.guava:guava:23.3-jre'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.13.0'
}
